# GIMUD - Graphical Interface Multi User Dungeon
# tom.npc: Datenbank fuer den Gerber Tom

Name = "Tom"
Outfit = (144,113-115-58-115-1)
Home = [32085,32199,7]
Radius = 1

Behaviour = {
ADDRESS,"hello$",! -> "I'm Tom the Tanner. How can I help you %N?"
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Please wait, %N. I'll call you in a minute.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Doh?"

"bye",male         -> "Good hunting, son.", Idle
"farewell",male    -> *
"bye",female       -> "Good hunting, child.", Idle
"farewell",female  -> *
"how","are","you" -> "Much to do, these days."
"job"             -> "I'm the local tanner. I buy fresh animal corpses, tan them, and convert them into fine leather clothes ...","I'm only selling to major customers. But I'm buying fresh corpses of rats, rabbits and wolves from you."
"name"            -> "My name is Tom the tanner."

"tanner"          -> "That's my job. I buy fresh animal corpses, tan them, and convert them into fine leather clothes."
"corpse"          -> "I'm buying fresh corpses of rats, rabbits and wolves. What do you want to sell?"
"animal"          -> *
"monster"         -> *
"wares"           -> *
"major"           -> "Yes. Obi, Norma and good old Al. Go ask them for leather clothes."
"customer"        -> *

"time"            -> "Sorry, I haven't been outside for a while, so I don't know."
"help"            -> "Help? I will give you a few gold coins if you have some dead animals for me."

"troll"           -> "Troll leather stinks. Can't use it."
"orc"             -> "I don't buy Orcs. Their skin is too scratchy."
"human"           -> "Are you crazy?!", Idle

"rat",Questvalue(224)=0     -> Type=3994, Amount=1, Price=2,  "I'll give you %P gold for a dead rat. Do you accept?", Topic=2
"rabbit",Questvalue(224)=0  -> Type=4173, Amount=1, Price=2,  "I'll give you %P gold for a dead rabbit. Do you accept?", Topic=2
"rat"                 -> Type=3994, Amount=1, Price=2,  "I'll give you %P gold for a dead rat. Do you accept?", Topic=1
"rabbit"              -> Type=4173, Amount=1, Price=2,  "I'll give you %P gold for a dead rabbit. Do you accept?", Topic=1
"wolf"                -> Type=4007, Amount=1, Price=5,  "Do you want to sell a dead wolf for %P gold?", Topic=1
"minotaur","leather"    -> Type=5878, Amount=1, Price=12,  "Do you want to sell a minotaur leather for %P gold?", Topic=1
"wolf","paw"    -> Type=5897, Amount=1, Price=7,  "Do you want to sell a wolf paw for %P gold?", Topic=1
"bear","paw"    -> Type=5896, Amount=1, Price=10,  "Do you want to sell a bear paw for %P gold?", Topic=1

%1,1<%1,"rat",Questvalue(224)=0     -> Type=3994, Amount=%1, Price=2*%1,  "I'll give you %P gold for %A dead rats. Do you accept?", Topic=2
%1,1<%1,"rabbit",Questvalue(224)=0  -> Type=4173, Amount=%1, Price=2*%1,  "I'll give you %P gold for %A dead rabbits. Do you accept?", Topic=2
%1,1<%1,"rat"     -> Type=3994, Amount=%1, Price=2*%1,  "I'll give you %P gold for %A dead rats. Do you accept?", Topic=1
%1,1<%1,"rabbit"  -> Type=4173, Amount=%1, Price=2*%1,  "I'll give you %P gold for %A dead rabbits. Do you accept?", Topic=1
%1,1<%1,"wolf"    -> Type=4007, Amount=%1, Price=5*%1,  "Do you want to sell %A dead wolves for %P gold?", Topic=1
%1,1<%1,"wolves"    -> Type=4007, Amount=%1, Price=5*%1,  "Do you want to sell %A dead wolves for %P gold?", Topic=1
%1,1<%1,"minotaur","leather"    -> Type=5878, Amount=%1, Price=12*%1,  "Do you want to sell %A minotaur leathers for %P gold?", Topic=1
%1,1<%1,"wolf","paw"    -> Type=5897, Amount=%1, Price=7*%1,  "Do you want to sell %A wolf paws for %P gold?", Topic=1
%1,1<%1,"bear","paw"    -> Type=5896, Amount=%1, Price=10*%1,  "Do you want to sell %A bear paws for %P gold?", Topic=1

Topic=1,"yes",Count(Type)>=Amount -> "Ok. Corpse for me, gold for you.", Delete(Type), CreateMoney
Topic=1,"yes"                             -> "Sorry, you do not have a fresh one."
Topic=1,"yes",Amount>1                    -> "Sorry, you do not have so many."
Topic=1                                   -> "Maybe another time."

Topic=2,"yes",Count(Type)>=Amount -> "Deal. By the way: If you'd like to hunt something bigger, check the cellar of the stables to the north. Some adventurer used to store his loot under a loose board beneath a barrel. He might have forgotten something when he left the isle.", Delete(Type), CreateMoney
Topic=2,"yes"                             -> "Sorry, you do not have a fresh one."
Topic=2,"yes",Amount>1                    -> "Sorry, you do not have so many."
Topic=2                                   -> "Maybe another time."

"sell"            -> "Sorry. I'm only selling to major customers. But I'm buying fresh corpses of rats, rabbits and wolves from you."


"addon",QuestValue(18504)=0,premium         -> "Would you like to wear bear paws like I do? No problem, just bring me 50 bear paws and 50 wolf paws and I'll fit them on.", SetQuestValue(18504,1)
"paws",QuestValue(18504)=0,premium      -> *
"addon",QuestValue(18504)=0         -> "Addons can be wear only by premium players."
"paws",QuestValue(18504)=0      -> *

"addon",QuestValue(18504)=1         -> "Have you brought 50 bear paws and 50 wolf paws?", Topic=3
"paws",QuestValue(18504)=1      -> *
Topic=3,"yes",Count(5897)>=50,Count(5896)>=50 -> "Excellent! Like promised, here are your bear paws.", DeleteAmount(5897,50), DeleteAmount(5896,50), SetQuestValue(18504,2), AddOutfitAddon(148,1), AddOutfitAddon(144,1), EffectOpp(13)
Topic=3,"yes"					           -> "You don't have required ingredients."
Topic=3                                   -> "Maybe another time."

"addon",QuestValue(18504)=2         -> "I see that you like your new bear paws!"
"paws",QuestValue(18504)=2      -> *

}
