# GIMUD - Graphical Interface Multi User Dungeon

Name = "Duncan"
Outfit = (151,38-23-0-116-1)
Home = [32330,32605,7]
Radius = 3

Behaviour = {
ADDRESS,"hello$",! -> "Hi %N, come closer. Have a look at my wares and such."
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Wait, %N.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Bye"

"bye" -> "Good bye.", Idle
"farewell" -> *
"do","you","sell" -> "Well erm, currently we are a little short on weapons and the like, but I still have pirate tapestries for sale."
"wares" -> *
"offer" -> *

"pirate tapestry" -> Type=5615, Amount=1,  Price=40,    "Do you want to buy a pirate tapestry for %P gold?", Topic=1
%1,1<%1,"pirate tapestry" -> Type=5615, Amount=%1, Price=40*%1, "Do you want to buy %A pirate tapestries for %P gold?", Topic=1

Topic=1,"yes",CountMoney>=Price -> "Here you go. Those would fit nicely into your house, showing that you're a fan and supporter and all that.", DeleteMoney, Create(Type)
Topic=1,"yes" -> "Come back when you have enough money."
Topic=1 -> "Maybe next time."

# Meriana_Quest
"mission",QuestValue(17520)=1,QuestValue(17523)=0 -> "I need a new quality atlas for our captains. Only one of the best will do it. I heard the explorers society sells the best, but only to members of a certain rank. You will have to get this rank or ask a high ranking member to buy it for you.", SetQuestValue(17523,1)
"task",QuestValue(17520)=1,QuestValue(17523)=0 -> *

"mission",QuestValue(17523)=1 -> Type=6108, Amount=1, "Did you get an atlas of the explorers society as I requested?", Topic=2
"task",QuestValue(17523)=1 -> *
"atlas",QuestValue(17523)=1 -> *
Topic=2,"yes",Count(Type)>=Amount -> "Indeed, what a fine work... the book I mean. Your work was acceptable all in all.", Delete(Type), SetQuestValue(17523,2)
Topic=2,"yes" -> "Sorry, you do not have it."
Topic=2 -> "Maybe another time."

"mission",QuestValue(17525)=0,QuestValue(17520)>4 -> "You did some impressive things. I think people here start considering you as one of us. But these are dire times and everyone of us is expected to give his best and even exceed himself. Do you think you can handle that?", Topic=3
"task",QuestValue(17525)=0,QuestValue(17520)>4 -> *
Topic=3,"yes" -> "I am glad to hear this. Please listen. The pirates on Nargor are breeding tortoises. They think eating tortoises makes a hard man even harder. ...",
				"However I am quite fond of tortoises and can't stand the thought of them being eaten. So I convinced Captain Striker that I can train them to help us. As a substitute for rafts and such ...",
				"All I need is one tortoise egg from Nargor. This is the opportunity to save a tortoise from a gruesome fate! ...",
				"I will ask Sebastian to bring you there. Travel to Nargor, find their tortoise eggs and bring me at least one of them.", SetQuestValue(17525,1)
Topic=3 -> "Maybe another time."

"mission",QuestValue(17525)=1 -> Type=6125, Amount=1, "Did you rescue one of those poor soon-to-be baby tortoises from Nargor?", Topic=4
"task",QuestValue(17525)=1 -> *
"egg",QuestValue(17525)=1 -> *
Topic=4,"yes",Count(Type)>=Amount -> "A real tortoise egg ... I guess you are more accustomed to rescue some noblewoman in distress but you did something good today.", Delete(Type), SetQuestValue(17525,2)
Topic=4,"yes" -> "Sorry, you do not have it."
Topic=4 -> "Maybe another time."

# Pirate_Outfit_Quest
"addon",QuestValue(17520)<12 -> "You're talking about my sabre? Well, first you have to earn our trust before you are granted to wear such a sabre."
"outfit",QuestValue(17520)<12 -> *
"addon",QuestValue(17567)<4,QuestValue(17520)=12 -> "You're talking about my sabre? Well, even though you earned our trust, you'd have to fulfil a task first before you are granted to wear such a sabre."
"outfit",QuestValue(17567)<4,QuestValue(17520)=12 -> *
"addon",QuestValue(17567)>3 -> "Firebird is the finest pirate sabre isin't it?"
"outfit",QuestValue(17567)>3 -> *

"task",QuestValue(17567)=0,QuestValue(17520)=12 -> "Are you up to the task which I'm going to give you and willing to prove you're worthy of wearing such a sabre?", Topic=5
"mission",QuestValue(17567)=0,QuestValue(17520)=12 -> *
Topic=5,"yes" -> "Listen, the task is not that hard. Simply prove that you are with us and not with the pirates from Nargor by bringing me some of their belongings. ...",
				"Bring me 100 of their eye patches, 100 of their peg legs and 100 of their hooks, in that order. ...",
				"Have you understood everything I told you and are willing to handle this task?", Topic=6
Topic=5 -> "Maybe another time."
Topic=6,"yes" -> "Good! Come back to me once you have gathered 100 eye patches.", SetQuestValue(17567,1), SetQuestValue(17594,1)
Topic=6 -> "Maybe another time."

"patch",QuestValue(17567)=1 -> Type=6098, Amount=100, "Have you gathered 100 eye patches?", Topic=7
"mission",QuestValue(17567)=1 -> *
"task",QuestValue(17567)=1 -> *
Topic=7,"yes",Count(Type)>=Amount -> "Good job. Alright, now bring me 100 peg legs.", Delete(Type), SetQuestValue(17567,2)
Topic=7,"yes" -> "You don't have that many."
Topic=7 -> "Maybe another time."

"leg",QuestValue(17567)=2 -> Type=6126, Amount=100, "Have you gathered 100 peg legs?", Topic=8
"mission",QuestValue(17567)=2 -> *
"task",QuestValue(17567)=2 -> *
Topic=8,"yes",Count(Type)>=Amount -> "Nice. Lastly, bring me 100 pirate hooks. That should be enough to earn your sabre.", Delete(Type), SetQuestValue(17567,3)
Topic=8,"yes" -> "You don't have that many."
Topic=8 -> "Maybe another time."

"hook",QuestValue(17567)=3 -> Type=6097, Amount=100, "Have you gathered 100 hooks?", Topic=9
"mission",QuestValue(17567)=3 -> *
"task",QuestValue(17567)=3 -> *
Topic=9,"yes",Count(Type)>=Amount -> "I see, I see. Well done. Go to Morgan and tell him this codeword: 'firebird'. He'll know what to do.", Delete(Type), SetQuestValue(17567,4)
Topic=9,"yes" -> "You don't have that many."
Topic=9 -> "Maybe another time."

"mission" -> "Sorry, I don't have any missions for you."
"task" -> *
}
