# GIMUD - Graphical Interface Multi User Dungeon
# galuna.npc: Datenbank für die Bogenmacherin Galuna

Name = "Galuna"
Outfit = (137,40-96-95-96-0)
Home = [32342,32246,6]
Radius = 2

Behaviour = {
ADDRESS,"hello$",! -> "Oh, please come in, %N. What do you need?"
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Sorry %N, I am already talking to a customer. Please wait", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Good bye."

"bye"       -> "Good bye.", Idle
"farewell"  -> *
"job"       -> "I am the local fletcher. I am selling bows, crossbows, and ammunition. Do you need anything?"
"fletcher"  -> *
"name"      -> "I am Galuna, paladin and fletcher."
"paladin"   -> "We are feared warriors and good marksmen. Ask Elane if want to know more about the guild."
"elane"     -> "She is the leader of all paladins."
"gorn"      -> "I supplied him with my goods in the past, now I sell them myself."
"time"      -> "Don't bother me. Go and buy a watch."
"tibia"     -> "Tibia, a green island. Here it is wunderful to walk into the forests and to hunt with a bow."
"forest"    -> *
"thais"     -> "We have visitors of all kind in Thais, only elves show up seldom."
"elf"       -> "It is rumored that they live in the northeast of Tibia. They are the best in archery."
"elves"     -> *

"buy"             -> "I am selling bows, crossbows, and ammunition. Do you need anything?"
"do","you","sell" -> *
"do","you","have" -> *
"offer"           -> *
"goods"           -> *
"ammo"            -> "Do you need arrows for a bow, or bolts for a crossbow?"
"ammunition"      -> *

"sell","bow"      -> "I don't buy used bows."
"sell","crossbow" -> "I don't buy used crossbows."
"power","bolt",QuestValue(17538)=0,male -> "Power bolts for you? Ha ha, boy you don't look like a real hunter to me."
"power","bolt",QuestValue(17538)=0,female -> "Power bolts for you? Ha ha, girl you don't look like a real hunter to me."

"bow"             -> Type=3350, Amount=1,  Price=400,  "Do you want to buy a bow for %P gold?", Topic=1
"crossbow"        -> Type=3349, Amount=1,  Price=500,  "Do you want to buy a crossbow for %P gold?", Topic=1
"arrow"           -> Type=3447, Amount=1,  Price=2,   "Do you want to buy an arrow for %P gold?", Topic=1
"bolt"            -> Type=3446, Amount=1,  Price=3,   "Do you want to buy a bolt for %P gold?", Topic=1
"power","bolt",QuestValue(17538)=1 -> Type=3450, Amount=1,  Price=10,   "Do you want to buy a power bolt for %P gold?", Topic=1

%1,1<%1,"bow"          -> Type=3350, Amount=%1, Price=400*%1, "Do you want to buy %A bows for %P gold?", Topic=1
%1,1<%1,"crossbow"     -> Type=3349, Amount=%1, Price=500*%1, "Do you want to buy %A crossbows for %P gold?", Topic=1
%1,1<%1,"arrow"        -> Type=3447, Amount=%1, Price=2*%1,   "Do you want to buy %A arrows for %P gold?", Topic=1
%1,1<%1,"bolt"         -> Type=3446, Amount=%1, Price=3*%1,   "Do you want to buy %A bolts for %P gold?", Topic=1
%1,1<%1,"power","bolt",QuestValue(17538)=1 -> Type=3450, Amount=%1, Price=10*%1,   "Do you want to buy %A power bolts for %P gold?", Topic=1

Topic=1,"yes",CountMoney>=Price -> "Here you are.", DeleteMoney, Create(Type)
Topic=1,"yes"                   -> "Come back, when you have enough money."
Topic=1                         -> "Hmm, but next time."

"task",QuestValue(17647)=0,paladin -> Amount=17648, "Young paladin, I see you need ammunition but those are too expensive, right. Hmm... I can't give you for free. ...",
									"However, if you could kill 50 minotaurs to prove your trustworthy willingness I will reward you the crossbow and 300 bolts. Deal?", Topic=120
									
"task",QuestValue(17648)=50,QuestValue(17647)=1,paladin -> "Well done, %N. Here is your crossbow and bolts!", SetQuestValue(17647,2), SetQuestValue(17649,0), Type=3349, Amount=1, Create(Type), Type=3446, Amount=300, Create(Type)
								
"task",QuestValue(17649)>0 -> "I see you are still in progress with your task."
									
Topic=120,"yes" -> "Very well young paladin. Come back once you are done.", SetQuestValue(17649,Amount), SetQuestValue(Amount,0), SetQuestValue(17647,1)
Topic=120 -> "As you wish."

"task" -> "I don't have any tasks for you right now."

}
