# GIMUD - Graphical Interface Multi User Dungeon

Name = "Klaus"
Outfit = (96,0-0-0-0-0)
Home = [31981,32826,2]
Radius = 5

Behaviour = {
ADDRESS,"hello$",! -> "Ho matey!", Topic=1
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Wait a moment, %N. I'll be here soon.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Whenever your throat is dry, you know where to find my tavern."

"bye"       -> "Whenever your throat is dry, you know where to find my tavern.", Idle
"farewell"  -> *
"name"       -> "I'm Klaus."
"rum"       -> "There are people who would kill you for rum. Then again there are people who would kill you anyway."
"raymond striker"       -> "Sooner or later he will run out of luck and then we will have his head."
"liberty bay"       -> "We have a kind of trade agreement. They make the rum and we plunder it. Har Har Har."
"thais"       -> "The Thaian people see themselves as the rulers of the world. If they only knew ... Har Har Har."
"carlin"       -> "A city that is run by women. And those wimps in Thais have still not been able to regain control. Har Har Har."
"venore"       -> "The Venoreans are the power of the future, trust me."
"king"       -> "If I were the king, I'd fill my court with the most beautiful women of the realm. Har Har Har."
"treasure map"       -> "I've already given you the complete map. Now you're on your own, matey."
"pirate"       -> "Ah, the romantic life of a pirate. Fish for breakfast, storms for lunch and an early death for supper. Har Har Har."
"excalibug"       -> "I guess it would come in handy to open some casks. Har Har Har."
"ferumbras"       -> "Dead magicians are only good for frightening little children. Har Har Har."

"gambl" -> "So you care for a civilized game of dice?", Topic=1
"game"  -> *
"dice"  -> *

Topic=1,"yes" -> "Hmmm, would you like to play for money or for a chance to win your own dice?", Topic=3
Topic=1,"no"  -> "Oh come on, don't be a child."

Topic=3,"money" -> "I thought so. Okay, I will roll a dice. If it shows 6, you will get five times your bet. How much do you want to bet?", Amount=Random(1,6), Topic=2
Topic=3,"dice"  -> "Hehe, good choice. Okay, the price for this game is 100 gold pieces. I will roll a dice. If I roll a 6, you can have my dice. Agreed?", Amount=Random(1,6), Topic=4

Topic=6,"yes" -> "Okay, I will roll a dice. If it shows 6, you will get five times your bet. How much do you want to bet?", Amount=Random(1,6), Topic=2
Topic=6,"no"  -> "Oh come on, don't be a child."
Topic=2,%1,0<%1,100>%1,CountMoney>=%1,Amount=6 -> Price=%1*5, "Ok, here we go ... 6! You have won %P, congratulations. One more game?", CreateMoney, EffectMe(27), Topic=6
Topic=2,%1,0<%1,100>%1,CountMoney>=%1          -> Price=%1, "Ok, here we go ... %A! You have lost. Har Har Har. One more game?", DeleteMoney, EffectMe(27), Topic=6
Topic=2,%1,0<%1,100>%1                         -> "You don't have so much money. How much do you want to bet?", Topic=2
Topic=2,%1                                     -> "I accept only bets between 1 and 99 gold. I don't want to ruin you after all. How much do you want to bet?", Topic=2

Topic=5,"yes" -> "Okay, the price for this game is 100 gold pieces. I will roll a dice. If I roll a 6, you can have my dice. Agreed?", Amount=Random(1,6), Topic=4
Topic=5,"no"  -> "Oh come on, don't be a child."
Topic=4,"yes",CountMoney>=100,Amount=6 -> Price=100, Type=5792, Amount=1, "Ok, here we go ... 6! You have won a dice, congratulations. One more game?", DeleteMoney, Create(Type), EffectMe(27), Topic=5
Topic=4,"yes",CountMoney>=100          -> Price=100, "Ok, here we go ... %A! You have lost. Har Har Har. One more game?", DeleteMoney, EffectMe(27), Topic=5
Topic=4,"yes"                          -> "You don't have so much money."

"mission",QuestValue(17520)=9,QuestValue(17531)=0 -> "Hmm, you look like a seasoned seadog. Kill Captain Ray Striker, bring me his lucky pillow as a proof and you are our hero!", SetQuestValue(17531,1)

"mission",QuestValue(17531)=1 -> Type=6105, Amount=1, "Do you have Striker's pillow?", Topic=7
"pillow",QuestValue(17531)=1 -> *
Topic=7,"yes",Count(Type)>=Amount -> "You DID it!!! Incredible! Boys, lets have a PAAAAAARTY!!!!", Delete(Type), SetQuestValue(17531,2)
Topic=7,"yes"                     -> "No you not!"
Topic=7                           -> "Maybe another time."

"mission"             -> "Sorry, I don't have any missions for you."
}
