# GIMUD - Graphical Interface Multi User Dungeon

Name = "Marina"
Outfit = (0,5811)
Home = [32329,32528,7]
Radius = 0

Behaviour = {
ADDRESS,"hello$",! -> "Oh, hello %N. A visitor, how nice!"
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Wait a second please, %N.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "How rude."

"bye"          -> "Good bye, %N.", Idle

"comb" -> "Sorry, I don't have a spare comb. I lost my favourite one when diving around in Calassa."

# The Mermaid Marina Quest
"silk","yarn",QuestValue(17504)=0 -> "Um. You mean, you really want me to touch that gooey spider silk just because you need yarn? Well... do you think that I'm pretty?", Topic=1
"spool","of","yarn",QuestValue(17504)=0 -> *
"spider","silk",QuestValue(17504)=0 -> *
Topic=1,"yes"                   -> "Well, everyone would say that in your position. Do you think that I'm really, absolutely the most stunning being that you have ever seen?", Topic=2
Topic=1							-> "Get out of there!"
Topic=2,"yes"                   -> "<giggles> It's funny how easy it is to get humans to say what you want. Now, proving it will be even more fun! ...",
								   "You want me to touch something gooey, so you have to touch something gooey for me too. <giggles> ...",
								   "I love honey and I haven't eaten it in a while, so bring me 50 honeycombs and worship my beauty a little more, then we will see.", SetQuestValue(17504,1), SetQuestValue(17595,1)
Topic=2							-> "Get out of there!"

"honeycomb",QuestValue(17504)=1 -> Type=5902, Amount=50, "Did you bring me the 50 honeycombs I requested and do you absolutely admire my beauty?", Topic=3
Topic=3,"yes",Count(Type)>=Amount -> "Oh goodie! Thank you! Okay... I guess since my fingers are sticky now anyway, I will help you. From now on, if you bring me 10 pieces of spider silk, I will create one spool of yarn.", Delete(Type), SetQuestValue(17504,2)
Topic=3,"yes"                     -> "Sorry, you do not have so many."
Topic=3                           -> "Maybe another time."

"silk","yarn",QuestValue(17504)=2 -> Type=5879, Amount=10, "Okay... a deal is a deal, would you like me to create a spool of yarn from 10 pieces of spider silk?", Topic=4
"spool","of","yarn",QuestValue(17504)=2 -> *
"spider","silk",QuestValue(17504)=2 -> *
Topic=4,"yes",Count(Type)>=Amount -> "Ew... gooey... there you go.", Delete(Type), Amount=1, Create(5886)
Topic=4,"yes"                     -> "Sorry, you do not have so many."
Topic=4                           -> "Maybe another time."

#The Shattered Isles Quest
"Raymond","Striker",QuestValue(17502)=7 -> "<giggles> I think he has a crush on me. Well, silly man, it is only for his own good. This way he can get accustomed to TRUE beauty. And I won't give him up anymore now that he is mine.", SetQuestValue(17502,8)

"date",QuestValue(17502)=9 -> "Is that the best you can do? A true Djinn would have done something more poetic.", SetQuestValue(17502,10)
"date",QuestValue(17502)=12 -> "This lovely, exotic Djinn is a true poet. And he is asking me for a date? Excellent. Now I can finaly dump this human pirate. He was growing to be boring more and more with each day ...", 
							   "As a little reward for your efforts I allow you to ride my sea turtles. Just look around at the shores and you will find them.", SetQuestValue(17502,13)
}
