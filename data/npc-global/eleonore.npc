# GIMUD - Graphical Interface Multi User Dungeon

Name = "Eleonore"
Outfit = (140,95-10-13-15-1)
Home = [32331,32763,5]
Radius = 2

Behaviour = {
ADDRESS,"hello$",! -> "Be greeted. What brings you here?"
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Please wait a minute, %N.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Oh well."

"bye"            -> "Good bye.", Idle
"farewell"       -> *

"ring",QuestValue(17502)=0         -> "My ring was stolen by a parrot, directly from my dressing table near the window. It flew to the nearby mountains and I fear my ring will be lost forever. Whoever returns it to me will be rewarded generously. ...",
									  "I guess that evil parrot hid the ring somewhere on a high tree or a rock so that you might need a rake to get it.", SetQuestValue(17502,1), SetQuestValue(17593,1)
"mission",QuestValue(17502)=0      -> *

"ring",QuestValue(17502)=1         -> Type=6093, Amount=1, Price=150, "Oh, my beloved ring! Have you found it and want to return it to me?", Topic=1
"mission",QuestValue(17502)=1      -> *

Topic=1,"yes",Count(Type)>=Amount -> "Oh, thank you so much! Take this gold as a reward. ... which reminds me, I would need some help in another matter. It is only a small errand. Are you interested?", Delete(Type), CreateMoney, SetQuestValue(17502,2), Topic=2
Topic=1,"yes"                   -> "You don't have it!"
Topic=1							-> "Too bad."

"ring",QuestValue(17502)=2         -> Price=200, "I would need some help in another matter. It is only a small errand. Are you interested?", Topic=3
"mission",QuestValue(17502)=2      -> *
"errand",QuestValue(17502)=2      -> *
Topic=2,"yes" -> Price=200, "Thank you! It is not a difficult matter but a rather urgent one. I need to send some money to a person in town. Would you be willing to run this small errand for me?", Topic=3
Topic=2                         -> "Too bad."
Topic=3,"yes" -> "I was hoping that you'd agree. Please deliver these 200 gold pieces to the herbalist Charlotta in the south-western part of the town. If you return from this errand, I will grant you 5 gold pieces as reward for your efforts.", CreateMoney, SetQuestValue(17502,3)
Topic=3                         -> "Too bad."

"mission",QuestValue(17502)=3      -> "Charlotta is still waiting for your delivery."
"errand",QuestValue(17502)=3      -> *

"mission",QuestValue(17502)=4      -> Price=5, "Great, thank you! As promised, here are your 5 gold pieces. Is there ... anything left that you might want to discuss with me?", CreateMoney, SetQuestValue(17502,5)
"errand",QuestValue(17502)=4      -> *

"peg","leg",QuestValue(17502)=5      -> "You have returned my ring and proven yourself as trustworthy. There is something I have to discuss with you. Are you willing to listen?", Topic=4
Topic=4,"yes" -> "I am glad to hear that. So please listen: Due to circumstances too complicated to explain now, I met Captain Ray Striker. He is ... a freedom fighter and would not find my father's acceptance, but we fell in love ...",
				 "Even though he had to hide for a while, we have stayed in contact for a long time now. And our love grew even further against all odds ...",
				 "However, recently we lost contact. I don't know what has happened to him and fear the worst ...",
				 "We always have been aware that something terrible might happen to him due to his lifestyle. But perhaps there is a harmless explanation for the absence of messages <holds her tears back>. I have arranged a passage for you to Ray's hiding place ...",
				 "Contact Captain Waverider, the old fisherman, and tell him the secret word 'peg leg'. He will make sure that you arrive safely ...",
				 "Please look for Ray and find out what happened to him and why he was not able to answer. Return to me as soon as you have found something out. I wish you a good journey.", SetQuestValue(17502,6)
Topic=4                         -> "Too bad."
"mission",QuestValue(17502)>5,QuestValue(17502)<13      -> "Return to me as soon as you have found something out about Ray."
"ray","striker",QuestValue(17502)=13      -> "<blushes> Oh, he is so wonderful. A very special man with a special place in my heart.", Topic=5
Topic=5,"mermaid"                         -> "I can't thank you enough for freeing my beloved Ray from that evil spell. I am still shocked that a mermaid could steal his love that easily.", SetQuestValue(17502,14)

}
