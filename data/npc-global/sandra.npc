# GIMUD - Graphical Interface Multi User Dungeon
# sandra.npc: Datenbank für die Trankhändlerin Sandra

Name = "Sandra"
Outfit = (140,115-95-127-76-1)
Home = [33258,31840,7]
Radius = 2

Behaviour = {
ADDRESS,"hello$",! -> "Hello %N."
ADDRESS,"hi$",!    -> *
ADDRESS,!          -> Idle
BUSY,"hello$",!    -> "Sorry %N, I am busy right now. Please wait a moment.", Queue
BUSY,"hi$",!       -> *
BUSY,!             -> NOP
VANISH,!           -> "Good bye and please come back soon."

"bye"       -> "Good bye and please come back soon.", Idle
"name"      -> "I am lady Sandra Astralian."
"job"       -> "I sell potions and magic fluids."
"time"      -> "Buy a watch."
"king"      -> "I was guest at his castle on my visits to Thais."
"tibianus"  -> *
"army"      -> "Do I look as if I'd fraternize with such people?"
"ferumbras" -> "A disgusting person indeed."
"excalibug" -> "I am not interested in tales only kids belive in."
"thais"     -> "A city full of disgusting people with ill manners."
"tibia"     -> "The world is a place of barbarianism."
"carlin"    -> "I plan to visit this city one day."
"edron"     -> "Isn't it a wonderful town?"
"news"      -> "Nothing I would talk to you about."
"rumors"    -> *

"offer"           -> "I'm selling life and mana fluids and several potions."
"goods"           -> *
"do","you","sell" -> *
"do","you","have" -> *
"fluid"           -> *
"potion"          -> "I'm selling potions of slime, blood, urine, oil, and distilled water."

"life","fluid"         -> Type=2874, Data=11, Amount=1,  Price=60,     "Do you want to buy life fluid for %P gold?", Topic=2
"mana","fluid"         -> Type=2874, Data=10, Amount=1,  Price=55,    "Do you want to buy mana fluid for %P gold?", Topic=2
%1,1<%1,"life","fluid" -> Type=2874, Data=11, Amount=%1, Price=60*%1,  "Do you want to buy %A potions of life fluid for %P gold?", Topic=2
%1,1<%1,"mana","fluid" -> Type=2874, Data=10, Amount=%1, Price=55*%1, "Do you want to buy %A potions of mana fluid for %P gold?", Topic=2
"slime"                -> Type=2874, Data=6,  Amount=1,  Price=12,     "Do you want to buy a potion of slime for %P gold?", Topic=2
"blood"                -> Type=2874, Data=5,  Amount=1,  Price=15,     "Do you want to buy a potion of blood for %P gold?", Topic=2
"urine"                -> Type=2874, Data=8,  Amount=1,  Price=10,     "Do you want to buy a potion of urine for %P gold?", Topic=2
"oil"                  -> Type=2874, Data=7,  Amount=1,  Price=20,     "Do you want to buy a potion of oil for %P gold?", Topic=2
"water"                -> Type=2874, Data=1,  Amount=1,  Price=8,      "Do you want to buy a potion of distilled water for %P gold?", Topic=2

"backpack","life","fluid" -> Type=2874, Data=11, Amount=1, Price=61*20, "Do you want to buy a backpack of life fluid for %P gold?", Topic=11
"bp","life","fluid" -> *
"backpack","mana","fluid" -> Type=2874, Data=10, Amount=1, Price=56*20, "Do you want to buy a backpack of mana fluid for %P gold?", Topic=11
"bp","mana","fluid" -> *

%1,1<%1,"backpack","life","fluid"  -> Type=2874, Data=11, Amount=%1, Price=61*20*%1, "Do you want to buy %A backpacks of potions of life fluid for %P gold?", Topic=11 
%1,1<%1,"bp","life","fluid"  -> *
%1,1<%1,"backpack","mana","fluid"  -> Type=2874, Data=10, Amount=%1, Price=56*20*%1, "Do you want to buy %A backpacks of potions of mana fluid for %P gold?", Topic=11 
%1,1<%1,"bp","mana","fluid"  -> *

"deposit",QuestValue(17545)=1                   -> "I will pay you 5 gold for every empty vial. Ok?", Data=0, Topic=3
"vial",QuestValue(17545)=1                      -> *
"flask",QuestValue(17545)=1                     -> *

Topic=2,"yes",CountMoney>=Price -> "Here you are. There is a deposit of 5 gold on the vial.", DeleteMoney, Create(Type)
Topic=2,"yes"                   -> "Come back, when you have enough money."
Topic=2                         -> "Hmm, but next time."

Topic=3,"yes",Count(2874)>0 -> Amount=Count(2874), Price=Amount*5, "Here you are ... %P gold.", Delete(2874), CreateMoney
Topic=3,"yes"               -> "You don't have any empty vials."
Topic=3                     -> "Hmm, but please keep Tibia litter free."

"mission",QuestValue(17520)=2 -> "Pssht, not that loud. So they have sent you to get... the stuff?", Topic=4
"fafnar","fire",QuestValue(17520)=2 -> *
Topic=4,"yes" -> "Finally. You have no idea how difficult it is to keep something secret here. And you brought me all the crystal coins I demanded?", Topic=5
Topic=4                           -> "How do you even know about the stuff then..."
Topic=5,"Your","continued","existence","is","payment","enough" -> "What?? How dare you?! I am a sorcerer of the most reknown academy on the face of this world. Do you think some lousy pirates could scare me? Get lost! Now! I will have no further dealings with the likes of you!", SetQuestValue(17520,3)
Topic=5                           -> "I see no deal with you then."

"deposit",QuestValue(17545)=0 -> "The Edron academy has introduced a bonus system. Each time you deposit 100 vials without claiming the money for it, you will receive a lottery ticket. ...",
								"Some of these lottery tickets will grant you a special potion belt accessory, if you bring the ticket to me. ...",
								"If you join the bonus system now, I will ask you each time you are bringing back 100 or more vials to me whether you claim your deposit or rather want a lottery ticket. ...",
								"Of course, you can leave or join the bonus system at any time by just asking me for the 'bonus'. ...",
								"Would you like to join the bonus system now?", Topic=6
"vial",QuestValue(17545)=0 -> *
"flask",QuestValue(17545)=0 -> *
Topic=6,"yes" -> "Great! I've signed you up for our bonus system. From now on, you will have the chance to win the potion belt addon!", SetQuestValue(17545,2)
Topic=6,"no" -> "Alright. I removed your name from our list. If you want to join again and get the chance to win a potion belt addon, just ask me for the 'bonus'.", SetQuestValue(17545,1)
Topic=6 -> "Maybe another time."

"deposit",QuestValue(17545)=2 -> "Would you like to get a lottery ticket instead of the deposit for your vials?", Data=0, Topic=7
"vial",QuestValue(17545)=2 -> *
"flask",QuestValue(17545)=2 -> *
Topic=7,"yes",Count(2874)>=100 -> "Ok! Here take this lottery ticket.", DeleteAmount(2874, 100), Type=5957, Amount=1, Create(Type)
Topic=7,"yes" -> "You don't have 100 empty vials."
Topic=7 -> "Maybe another time."

"bonus",QuestValue(17545)=2 -> "Would you like to leave our bonus system?", Topic=8
Topic=8,"yes" -> "Alright. I removed your name from our list. If you want to join again and get the chance to win a potion belt addon, just ask me for the 'bonus'.", SetQuestValue(17545,1)
Topic=8 -> "Alright. Your name is still in our bonus system list."

"bonus",QuestValue(17545)=1 -> "Would you like to join our bonus system?", Topic=9
Topic=9,"yes" -> "Alright. I added your name from in to the bonus system list. Wish you best of luck on depositing vials!", SetQuestValue(17545,2)
Topic=9 -> "Maybe another time."

"winning","lottery","ticket",QuestValue(17546)=0 -> Type=5958, Amount=1, "Are you here to claim a prize?", Topic=10
"prize",QuestValue(17546)=0 -> *
Topic=10,"yes",Count(Type)>=Amount -> "Congratulations! Here, from now on you can wear our lovely potion belt as accessory.", Delete(Type), SetQuestValue(17546,1), SetQuestValue(17594,1), AddOutfitAddon(138,1), AddOutfitAddon(133,1), EffectOpp(13)
Topic=10,"yes" -> "Where is yours winning lottery ticket then?"
Topic=10 -> "Maybe another time."

"winning","lottery","ticket",QuestValue(17546)=1 -> "I am sorry, but since you already have our potion belt we have no further prizes left for you. Maybe try to gift the winning lottery ticket for someone who really want to claim the accessory."
"prize",QuestValue(17546)=1 -> *

Topic=11,"yes",CountMoney>=Price -> "Here you are. There is a deposit of 5 gold on the vial.", DeleteMoney, CreateContainer(2854,Type,Data)
Topic=11,"yes"                   -> "Come back, when you have enough money."
Topic=11                         -> "Hmm, but next time."
}
